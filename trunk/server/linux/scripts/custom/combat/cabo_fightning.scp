[EVENTS e_cs_combatPVP]
on=@Attack
	I.TAG.attackTo = <SRC.UID> //Guarda a quien est치s atacando.
		
ON=@Hit
	//Nunca hubo ejecuci칩n (0)
	IF !(<I.TAG0.combatFail>)
		I.TAG0.combatFail = 2 //Decimos, simplemente, que hemos pasado por hit.
		I.TRIGGER @hitmiss
	ENDIF
	
	//Hubo ejecuci칩n y el resultado fue de fallo.
	IF (<I.TAG.combatFail> == 1)
		I.TAG.combatFail =
		RETURN 1
	ENDIF
	I.TAG.combatFail =
	
	//Continuar치 si es de acierto (!= 0 && != 1 && == 2)

	//Zona de golpeo
	LOCAL.zona = <EVAL { cs_zone_head armor_helm_percentage cs_zone_neck armor_gorget_percentage cs_zone_chest armor_chest_percentage cs_zone_back armor_back_percentage cs_zone_arms armor_arms_percentage cs_zone_hands armor_gloves_percentage cs_zone_legs armor_legs_percentage cs_zone_feet armor_shoes_percentage }>
	
	IF (<SRC.BRAIN>) && (<SRC.BODY> != c_man) && (<SRC.BODY> != c_woman)
		//Si es un NPC y no es humano
		LOCAL.armorZone = <SRC.f_cs_armor_npcnohuman_calc <LOCAL.zona> <SRC.AR>>
	ELSE
		LOCAL.armorZone = <f_ar_zone <SRC.UID> <EVAL <LOCAL.zona>> >
	ENDIF
	
	I.SYSMESSAGE @33,1 "** Sistema de Combate **"
	
	I.SYSMESSAGE @16,1 "--Enemigo--"
	I.SYSMESSAGE @16,1 Zona que golpeo: <EVAL <LOCAL.zona>>
	I.SYSMESSAGE @16,1 Armor en esa zona : <EVAL <LOCAL.armorZone>>
	I.SYSMESSAGE @16,1 Resistencias : Fuego: <EVAL <SRC.RESFIRE>> Frio: <EVAL <SRC.RESCOLD>> Veneno: <EVAL <SRC.RESPOISON>> Energia: <EVAL <SRC.RESENERGY>>
		
	LOCAL.daFinal = 0
	IF <ARGO.ISWEAPON>
		//Fisical damage
		IF !(<isVacio <ARGO.TAG0.DMGFISIC> >)
			LOCAL.daFis =  <I.f_cs_fighting_calc <max <ARGO.TAG0.DMGFISIC>> <min <ARGO.TAG0.DMGFISIC>> <LOCAL.armorZone>>
		ENDIF
		
		//FireDamage
		IF !(<isVacio <ARGO.TAG0.DMGFIRE> >)
			LOCAL.daFire = <I.f_cs_fighting_calc_elem <max <ARGO.TAG0.DMGFIRE>> <min <ARGO.TAG0.DMGFIRE>> <SRC.RESFIRE>>
		ENDIF
		
		//Cold Damage
		IF !(<isVacio <ARGO.TAG0.DMGCOLD> >)
			LOCAL.daCold = <I.f_cs_fighting_calc_elem <max <ARGO.TAG0.DMGCOLD>> <min <ARGO.TAG0.DMGCOLD>> <SRC.RESCOLD>>
		ENDIF
		
		//Poison Damage
		IF !(<isVacio <ARGO.TAG0.DMGPOISON> >)
			LOCAL.daPoison = <I.f_cs_fighting_calc_elem <max <ARGO.TAG0.DMGPOISON>> <min <ARGO.TAG0.DMGPOISON>> <SRC.RESPOISON>>
		ENDIF
		
		//Energy Damage
		IF !(<isVacio <ARGO.TAG0.DMGENERGY> >)
			LOCAL.daEnergy = <I.f_cs_fighting_calc_elem <max <ARGO.TAG0.DMGENERGY>> <min <ARGO.TAG0.DMGENERGY>> <SRC.RESENERGY>>
		ENDIF
		
		I.SYSMESSAGE @100,2 "--Danyo Final--"
		I.SYSMESSAGE @100,2 Fisico: <EVAL <LOCAL.daFis>> -- Fuego: <EVAL <LOCAL.daFire>> -- Frio: <EVAL <LOCAL.daCold>> -- Veneno: <EVAL <LOCAL.daPoison>> -- Energy: <EVAL <LOCAL.daEnergy>>
		
		LOCAL.daFinal = <EVAL <LOCAL.daFis> + <LOCAL.daFire> + <LOCAL.daCold> + <LOCAL.daPoison> + <LOCAL.daEnergy> >
		
		//Sonidos
		IF (<I.WEAPON.TYPE> == t_weapon_sword)
			SOUND {snd_weapons_sword1  1 snd_weapons_sword7 1}
		ELSEIF (<I.WEAPON.TYPE> == t_weapon_fence )
			SOUND {snd_weapons_sword1  1 snd_weapons_sword7 1} 
		ELSEIF (<I.WEAPON.TYPE> == t_weapon_mace_pick) || (<I.WEAPON.TYPE> == t_weapon_mace_smith) || (<I.WEAPON.TYPE> == t_weapon_mace_sharp) || (<I.WEAPON.TYPE> == t_weapon_mace_staff) || (<I.WEAPON.TYPE> == t_weapon_mace_crook)|| (<I.WEAPON.TYPE> == t_weapon_axe)
			SOUND {snd_weapons_hvyswrd1  1 snd_weapons_hvyswrd4 1}
		ELSEIF (<I.WEAPON.TYPE> == t_weapon_bow)
			SOUND snd_weapons_crossbow
		ENDIF
			
	ELSE //Si no tiene arma.
		LOCAL.daFinal = <f_boxeo_calc <I.UID> <LOCAL.armorZone>>
	ENDIF
	
	I.SYSMESSAGE @100,1 Danyo Final: <EVAL <LOCAL.daFinal>>
	
	SRC.DAMAGE <LOCAL.daFinal> , <ARGO.TYPE> , <I.UID>
	
	f_checkSkill <I.UID> 40
	
	RETURN 1

ON=@hitmiss
	//Hit se ha ejecutado alguna vez?
	IF !(<I.TAG0.combatFail>)
		LOCAL.execHit = 1
	ELSE
		LOCAL.execHit = 0
	ENDIF
	
	IF <I.WEAPON.ISWEAPON>
		IF (<I.WEAPON.TYPE> == t_weapon_sword) || (<I.WEAPON.TYPE> == t_weapon_axe)
			FLOAT.Harma = <I.swordsmanship>
		ELSEIF <I.WEAPON.TYPE> == t_weapon_fence
			FLOAT.Harma = <I.Fencing>
		ELSEIF (<I.WEAPON.TYPE> == t_weapon_mace_pick) || (<I.WEAPON.TYPE> == t_weapon_mace_smith) || (<I.WEAPON.TYPE> == t_weapon_mace_sharp) || (<I.WEAPON.TYPE> == t_weapon_mace_staff) || (<I.WEAPON.TYPE> == t_weapon_mace_crook)
			FLOAT.Harma = <I.Macefighting>
		ELSEIF (<I.WEAPON.TYPE> == t_weapon_bow)
			FLOAT.Harma = <I.Archery>
		ENDIF
	ELSE
		FLOAT.Harma = <I.WRESTLING>
	ENDIF

	FLOAT.hanatomia = <I.Anatomy>
	FLOAT.htacticas = <I.Tactics>
	IF <I.FOOD> < <VAR.factorHungry>
		LOCAL.pAcierto = <FEVAL <FLOATVAL (((2*<FLOAT.Harma>)+<FLOAT.hanatomia>+<FLOAT.htacticas>)/4)*(<I.FOOD>/<VAR.factorHungry>)> >
	ELSE
		LOCAL.pAcierto = <FEVAL <FLOATVAL (((2*<FLOAT.Harma>)+<FLOAT.hanatomia>+<FLOAT.htacticas>)/4)> >
	ENDIF

	LOCAL.pAcierto = <EVAL 100 - {<EVAL 100 - <LOCAL.pAcierto>> 0} >

	I.SYSMESSAGE probabilidad Player: <LOCAL.pAcierto>

	IF {0 100} < <LOCAL.pAcierto>
		I.TAG.combatFail = 2 //Acierto
		SAY Acerte...
	ELSE
		I.TAG.combatFail = 1 //Fallo
		SAY falle...
		SOUND {snd_weapons_swish01 1 snd_weapons_swish02 1 snd_weapons_swish03 1 snd_weapons_blunt01 1 snd_weapons_axe01 1}
	ENDIF
	
	IF (<LOCAL.execHit>)
		I.TRIGGER @hit //Se ejecuta Hit sino se ha ejecutado anteriormente.
	ENDIF
	
	RETURN 1


[EOF]
